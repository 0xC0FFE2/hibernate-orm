/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */
import org.hibernate.orm.jakarta.JakartaJarTransformation

description = 'Wildfly Transaction Client transformed to be JTA 2.0 compatible'

apply from: rootProject.file( 'gradle/published-java-module.gradle' )

tasks.withType(PublishToMavenRepository) {
	onlyIf {
		repository.name == "MavenLocal"
	}
}

configurations {
	wildFlyTxnClient {
		description = 'Used to access the WildFly transaction client jar to be able to transform it'
	}
}

dependencies {
	api libraries.jakarta_jta

	wildFlyTxnClient( libraries.wildfly_transaction_client ) {
		transitive = false;
	}
}

tasks.compileJava.enabled = false
tasks.processResources.enabled = false
tasks.compileTestJava.enabled = false
tasks.processTestResources.enabled = false
tasks.test.enabled = false
tasks.jar.enabled = false

task jakartafyDependency(type: JakartaJarTransformation) {
	sourceJar = project.provider({
		project.configurations.wildFlyTxnClient.resolvedConfiguration.resolvedArtifacts.find().file
	})

	targetJar = tasks.jar.archiveFile
}